group 'com.github.pritamsaha92'
version 'v6.18.3'

apply plugin: 'java'
apply plugin: 'maven-publish'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

ext {
    cucumberVersion       = '7.20.1'
    testNGVersion         = '7.5'
    jacksonVersion        = '2.17.0'
    httpcomponentsVersion = '4.5.13'
    extentreportVersion   = '5.0.3'
    mustacheVersion       = '0.9.6'
    gsonVersion           = '2.11.0'
}

repositories {
    mavenCentral()
}

dependencies {
    compile "io.cucumber:cucumber-core:$cucumberVersion"
    compile "io.cucumber:cucumber-java:$cucumberVersion"
    compile "io.cucumber:cucumber-java8:$cucumberVersion"
    compile "io.cucumber:cucumber-junit:$cucumberVersion"
    compile("io.cucumber:cucumber-testng:$cucumberVersion") {
        exclude group: 'com.google.guava'
        exclude group: 'org.testng'
    }
    compile "org.testng:testng:$testNGVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"
    compile "com.fasterxml.jackson.module:jackson-module-parameter-names:$jacksonVersion"
    compile "org.apache.httpcomponents:httpclient:$httpcomponentsVersion"
    compile "org.apache.httpcomponents:httpmime:$httpcomponentsVersion"
    compile "com.aventstack:extentreports:$extentreportVersion"
    compile "com.github.spullara.mustache.java:compiler:$mustacheVersion"
    compile "com.google.code.gson:gson:$gsonVersion"
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jar {
    manifest {
        attributes 'Automatic-Module-Name': 'com.github.pritamsaha92.courgette.jvm'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact tasks.named("sourcesJar").get()
            artifact tasks.named("javadocJar").get()

            pom {
                name        = 'courgette-jvm'
                description = 'Fork of courgette-jvm (Java 8 compatible) by pritamsaha92'
                url         = 'https://github.com/pritamsaha92/courgette-jvm'

                scm {
                    connection          = 'scm:git:git://github.com/pritamsaha92/courgette-jvm.git'
                    developerConnection = 'scm:git:ssh://github.com/pritamsaha92/courgette-jvm.git'
                    url                 = 'https://github.com/pritamsaha92/courgette-jvm'
                }

                developers {
                    developer {
                        id    = 'pritamsaha92'
                        name  = 'Pritam Saha'
                        email = 'pritampro.ju@gmail.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = 'GitHubPackages'
            url  = uri("https://maven.pkg.github.com/pritamsaha92/courgette-jvm")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
}
